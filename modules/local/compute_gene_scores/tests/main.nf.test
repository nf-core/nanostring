nextflow_process {

    name "Test Process COMPUTE_GENE_SCORES"
    script "../main.nf"
    process "COMPUTE_GENE_SCORES"
    config "./nextflow.config"

    tag "modules"
    tag "compute_gene_scores"

    test("test profile") {
        when {
            process {
                """
                input[0] = [[id:"test"], file(params.pipelines_testdata_base_path + 'nanostring/modules_test_data/test_profile/normalized_counts.tsv', checkIfExists: true )]
                input[1] = file(params.pipelines_testdata_base_path + 'nanostring/gene_score_test.yaml', checkIfExists: true )
                """
            }
        }
        then {
            assertAll(
                { assert process.success },
                {
                    def files = process.out.scores_for_mqc[0][1]
                    def signatureScoresFile = files.find { file(it).name.contains('signature_scores_mqc') }
                    def signatureQcFile = files.find { file(it).name.contains('signature_scores_qc_mqc') }

                    // Round floating-point numbers to 2 decimal places
                    def scoresContent = file(signatureScoresFile).text.replaceAll(/(-?\d+\.\d+)/) { match ->
                        String.format("%.2f", Double.parseDouble(match[1]))
                    }

                    assert snapshot(
                        scoresContent,
                        file(signatureQcFile).text
                    ).match()
                }
            )
        }
    }

    test("stub") {
        options '-stub'
        when {
            process {
                """
                input[0] = [[id:"test"], file(params.pipelines_testdata_base_path + 'nanostring/modules_test_data/test_profile/normalized_counts.tsv', checkIfExists: true )]
                input[1] = file(params.pipelines_testdata_base_path + 'nanostring/gene_score_test.yaml', checkIfExists: true )
                """
            }
        }
        then {
            assertAll(
                { assert process.success },
                { assert snapshot(process.out).match() },
            )
        }
    }
}
